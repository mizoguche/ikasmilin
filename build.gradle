buildscript {
    ext.kotlin_version = '1.1.4-3'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

        // For spek.gradle
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.M11"
}

detekt {
    version = "1.0.0.M11"
    input = "$input/src/main/kotlin"
    config = "$input/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url 'https://jitpack.io' }
    }

    group = 'info.mizoguche.ikasmilin'
    version = '0.1.0'
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.8.1'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}


task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    compile 'com.github.kittinunf.fuel:fuel:1.10.0'
    compile 'com.github.kittinunf.fuel:fuel-gson:1.10.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'info.mizoguche.ikasmilin:ikasmilin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mizoguche/ikasmilin.git'

        version {
            name = '0.1.0'
            desc = 'Result API'
            released  = new Date()
            vcsTag = '0.1.0'
        }
    }
    publications = ['BintrayPublication']
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            groupId 'info.mizoguche'
            artifactId 'ikasmilin'
            version '0.1.0'
        }
    }
}